

<program> ::= <statement>*

<statement> ::= "let" <identifier> "=" <expr> ";"                  // הצהרת משתנה
              | <identifier> "=" <expr> ";"                        // השמה מחדש
              | "print" <expr> ";"                                 // הדפסה
              | "if" "(" <expr> ")" <block> ["else" <block>]      // תנאי
              | "while" "(" <expr> ")" <block>                     // לולאה
              | <function_decl>                                    // הגדרת פונקציה
              | "return" <expr> ";"                                // החזרת ערך מתוך פונקציה

<function_decl> ::= "funtion" <identifier> "(" [<identifier_list>] ")" <block>

<identifier_list> ::= <identifier> ("," <identifier>)*

<block> ::= "{" <statement>* "}"

<expr> ::= <logic_or>

<logic_or> ::= <logic_and> ( "||" <logic_and> )*
<logic_and> ::= <equality> ( "&&" <equality> )*
<equality> ::= <comparison> ( ("==" | "!=") <comparison> )*
<comparison> ::= <term> ( ("<" | "<=" | ">" | ">=") <term> )*
<term> ::= <factor> ( ("+" | "-") <factor> )*
<factor> ::= <unary> ( ("*" | "/") <unary> )*
<unary> ::= ("!" | "-") <unary> | <primary>
<primary> ::= <number>
            | <string>
            | <boolean>
            | <identifier>
            | <function_call>
            | "(" <expr> ")"

<function_call> ::= <identifier> "(" [<arg_list>] ")"
<arg_list> ::= <expr> ("," <expr>)*

<number> ::= regex:\d+
<string> ::= regex:"[^"]*"
<boolean> ::= "true" | "false"
<identifier> ::= regex:[a-zA-Z_][a-zA-Z0-9_]*
